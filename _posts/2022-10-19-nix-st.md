# st - the suckless terminal (or the simple terminal)

## Install on Nixos

### install system-wide

Open configuration.nix and add it to your environment.systemPacages attribute :

environment.systemPackages = with pkgs; [
  st
];

### Install for a certain user 

users.users.alice.packages = with pkgs; [
  st
];

# Install on a non-nixos distro ; with the nix package manager ; using nix-env not a flake

Open a file , write a nix expression, e.g. :

vim my-st

```
# pin nixpkgs at a particular release

{ pkgs ? import (fetchTarball "https://github.com/NixOS/nixpkgs/archive/3590f02e7d5760e52072c1a729ee2250b5560746.tar.gz") {}
}:

pkgs.st.overrideAttrs {  }          # <--  put stuff in these brackets

```

nix-env -i -f my-custom-st


# Customize st with a patch; st, and the suckless software in general, is customized using patches 

## Get the patch's hash 

e.g.

nix-prefetch-url https://st.suckless.org/patches/rightclickpaste/st-rightclickpaste-0.8.2.diff

## Apply the patch to st

Apply the patch using 'overrideAttrs' (look up the use of this overrideAttrs here) :

### Option 1: The patch / diff to apply is in a local dir
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    patches = [
      ./path/to/local.diff                      
    ];
  }))
];
```

### Option 2a: The patch / diff to apply is served from a web server, e.g. here I'm getting it from st.suckless.org (there are many patches here):
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    patches = [
      (fetchpatch {
        url = "https://st.suckless.org/patches/rightclickpaste/st-rightclickpaste-0.8.2.diff";
        sha256 = "1y4fkwn911avwk3nq2cqmgb2rynbqibgcpx7yriir0lf2x2ww1b6";
      })                                        
    ];
  }))
];
```

### Option 2b: The patch / diff to apply is served from a web server, e.g. here I'm getting it from github:
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    patches = [
      (fetchpatch {
        url = "https://raw.githubusercontent.com/fooUser/barRepo/1111111/somepatch.diff";
        sha256 = "222222222222222222222222222222222222222222";
      })
    ];
  }))
];
```

## Help, my patch has a dependency / requirement 

Put the requirement in the buildInputs attribute, e.g.: 
The st-ligatures customization / patch requires me to have harfbuzz installed!
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    buildInputs = oldAttrs.buildInputs ++ [ harfbuzz ];
    patches = [
      (fetchpatch {
        url = "https://st.suckless.org/patches/ligatures/0.8.3/st-ligatures-20200430-0.8.3.diff";
        sha256 = "67b668c77677bfcaff42031e2656ce9cf173275e1dfd6f72587e8e8726298f09";
      })
    ];
  }))
];
```

## How do I configure st ? 

Use a file called config.h (a .h file is called a header file):

### Option 1 : use a local file

(in these two options we are using writeText , a function which writes a file to the store and gives you the path to it.) see my write up on writeText vs writeBin
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    configFile = writeText "config.def.h" (builtins.readFile ./path/to/local/config.h);
    postPatch = "${oldAttrs.postPatch}\n cp ${configFile} config.def.h";
  }))
];
```

### Option 2 : use a file that is in github
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    configFile = writeText "config.def.h" (builtins.readFile "${fetchFromGitHub { owner = "LukeSmithxyz"; repo = "st"; rev = "8ab3d03681479263a11b05f7f1b53157f61e8c3b"; sha256 = "1brwnyi1hr56840cdx0qw2y19hpr0haw4la9n0rqdn0r2chl8vag"; }}/config.h");
    postPatch = "${oldAttrs.postPatch}\n cp ${configFile} config.def.h";
  }))
];
```

## Complete example of st with patch --> including a dependency of patch, fetching the patch from web server, and applying a config file
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    buildInputs = oldAttrs.buildInputs ++ [ harfbuzz ];           # ligatures dependency
    patches = [
      # ligatures patch
      (fetchpatch {
        url = "https://st.suckless.org/patches/ligatures/0.8.3/st-ligatures-20200430-0.8.3.diff";
        sha256 = "67b668c77677bfcaff42031e2656ce9cf173275e1dfd6f72587e8e8726298f09";
      })
    ];
    # config file
    configFile = writeText "config.def.h" (builtins.readFile "${fetchFromGitHub { owner = "me"; repo = "my-custom-st-stuff"; rev = "1111222233334444"; sha256 = "11111111111111111111111111111111111"; }}/config.h");
    postPatch = oldAttrs.postPatch ++ ''cp ${configFile} config.def.h'';
  }))
];
```

## Another option to patches! use your own forked git repo 

Fork the mainline repository.
Here is an example using Luke Smith's st fork:

override the src attribute:

### Option 1: Use a fetchfromGithub to get the repo 
```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    buildInputs = oldAttrs.buildInputs ++ [ harfbuzz ];   # as before, specify dependencies needed for the build 
    src = fetchFromGitHub {
      owner = "LukeSmithxyz";
      repo = "st";
      rev = "8ab3d03681479263a11b05f7f1b53157f61e8c3b";
      sha256 = "1brwnyi1hr56840cdx0qw2y19hpr0haw4la9n0rqdn0r2chl8vag";
    };
  }))
];
```

### Option 2: Use a tarball -> advantage over option 1 is that the tarball is kept up to date. See the post about fetchTarball vs fetchFromGitHub

```
environment.systemPackages = with pkgs; [
  (st.overrideAttrs (oldAttrs: rec {
    buildInputs = oldAttrs.buildInputs ++ [ harfbuzz ];   # as before, specify dependencies needed for the build 
  src = builtins.fetchTarball {
    url = "https://github.com/lukesmithxyz/st/archive/master.tar.gz";
  };
  }))
];
```

# Test the patch

## First get / fetch the patch

e.g. create this file st-test.nix to first get / fetch the patch ..

```
with import <nixpkgs> {};

(st.overrideAttrs (oldAttrs: rec {
  buildInputs = oldAttrs.buildInputs ++ [ harfbuzz ];
  patches = [
    (fetchpatch {
      url = "https://st.suckless.org/patches/ligatures/0.8.3/st-ligatures-20200430-0.8.3.diff";
      sha256 = "18fllssg5d5gik1x0ppz232vdphr0y2j5z8lhs5j9zjs8m9ria5w";
    })
  ];
}))
```

## Run the file in a nix shell

nix-shell st-test.nix

## Run the various phases to see if it works. See my guide about the different phases

unpackPhase

ls

cd theunpackeddir

patchPhase

Additional phases

# Note 

This current page is also a useful guide for generally patching files in nix.
